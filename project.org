
* ADC values

  Fibretronics wearlink is a string of push button (no) switches individually
  in series with a resistor. When no button is pressed it is open
  circuit. When one button is pressed, the resistance corresponding to that
  key is present between the two feed wires.

  To determine the value for the adc we calculate ideal adc reading based on
  the known/measured resistance per button.

  Additional wrinkles;
  - ADC reference is VBG = 1.2V
  - ADC prescaling = 1/3
  - ADC input impendance is ~390k
  - ADC sampling time is 20us @ 8 bits, 68us @ 10 bit

  Schematic:

   Vcc--Rk---x----Rl--gnd
             |
            Ra
             |
           Vref/2

   Where:
   Rk = key resistance
   Rl = limit or load resistance, makes a Rk + Rl load when not sampling
   Ra = the adc impedance at sampling time
   Ua = voltage at point x

   Ik = Il + Ia = Vcc / Rk
   Ia = (Ua - (Vref / 2)) / Rain
   Il = Ua / Rl

   Solve for Ua: Ua = (-Vcc - ((Vref / 2) / Ra)) / (-1 - (Rk / Ra) - (Rk / Rl))

  | ! | Key           |       Rk |         Uau |      Ua |   Vscaled |      Adc |   A-D% |    A+D% |
  |   |               |          |   unsampled | sampled |           |          |        |         |
  |---+---------------+----------+-------------+---------+-----------+----------+--------+---------|
  | # | KEY_NONE      | 10000000 |       0.007 |   0.036 |     0.012 |        3 |      3 |       3 |
  | # | KEY_VOLUMEDEC |    10000 |       2.200 |   2.173 |     0.724 |      154 |    146 |     162 |
  | # | KEY_REWIND    |    20000 |       1.650 |   1.624 |     0.541 |      115 |    109 |     121 |
  | # | KEY_PLAY      |    47000 |       0.985 |   0.972 |     0.324 |       69 |     66 |      72 |
  | # | KEY_FORWARD   |    30000 |       1.320 |   1.298 |     0.433 |       92 |     87 |      97 |
  | # | KEY_VOLUMEINC |     5600 |       2.578 |   2.556 |     0.852 |      182 |    173 |     191 |
  |---+---------------+----------+-------------+---------+-----------+----------+--------+---------|
  | $ | Vcc=3.3       | Rl=20000 | Rain=389200 |         | scale=1/3 | Vref=1.2 | bits=8 | dev=.05 |
  #+TBLFM: $4=($Rl/($Rk+$Rl))*$Vcc;%.3f::$5=(-(($Vref/2)*$3)/$Rain-$Vcc)/((-$3/$Rain)-($3/$Rl)-1);%.3f::$6=$Ua*$scale;%.3f::$7=$Vscaled/($Vref/2^$bits);%.0f::$8=max($7-($dev*$7),0);%.0f::$9=min($7+($dev*$7),2^$bits);%0.f

